

bomlev -2

! SHOULD BE ABLE TO SET WHEN WRITING THE SCRIPT
! Will this pick velocities from the correct distribution?

set NMIN %Nmin
set NMAX %Nmax
set ALPHA %Alpha
set LOWERBBASIN %LowerBbasin
set HIGHERBBASIN %UpperBbasin
set STEPNUM %Stepnum

!=================================
!Read topology and parameter files
!=================================

open unit 1 form read name "%topology_read_in_file"
read rtf card unit 1
close unit 1

open unit 1 form read name "%parameter_read_in_file"
read param card unit 1
close unit 1

set temp = 300


!=================================
!Read current psf and coordinates
!=================================

open read unit 30 card name "%psf_read_in_file"
read psf unit 30 card
close unit 30

open read unit 30 card name "%crd_read_in_file"
read coor unit 30 card
close unit 30

open read unit 47 file name "%dcd_read_in_file"
trajectory query unit 47
calc ntst = ?skip
calc begst = @ntst*%Num_to_pick
calc stopst = @ntst*%Num_to_pick
trajectory firstu 47 nunit 1 skip @ntst begin @begst stop @stopst
traj read
close unit 47

open write unit 30 card name "trajreadin.crd"
write coor unit 30 card
close unit 30

!=================================
!Set quantum regions
!=================================

%Quantum_region

!=================================
!Set rxncoor
!=================================

%rxncoor

!=================================
!Set up umbrella center with a zero force constant so we can trace it
!=================================

%rxncoor_zero_umbrella

!=================================
!NMIN trajectories dynamics
!=================================

open write unit 341 card name rsum1_test.txt
open write unit 342 card name rsum2_test.txt

rxncor: trace rone unit 341 freq 1
rxncor: trace rtwo unit 342 freq 1

close unit 341
close unit 342

set COUNTER 0
 
label REPLOOP

open read unit 30 card name "trajreadin.crd"
read coor unit 30 card
close unit 30

RAND OLDR
calc theseed = ?rand * %Random_inp_num

open write unit 341 card name rsum1_@COUNTER.txt
open write unit 342 card name rsum2_@COUNTER.txt

Dynamics Leap -
       finalt 300 firstt 300 ichecw 0 ihbfrq 0 ilbfrq 0 -
       inbfrq -1 iprfrq 10 iscale 0 iseed @theseed iuncrd -1 -
       iunread -1 iunvelo -1 iunwrite -1 nprint 50 nsavcrd 1 -
       nsavvelo 0 nstep @STEPNUM scale 1 tbath 300 teminc 0 -
       timestp 0.001 twindh 0 twindl 0 

open write unit 30 card name "final_@COUNTER.crd"
write coor unit 30 card
close unit 30

close unit 341
close unit 342

increment COUNTER by 1
if COUNTER .LT. @NMIN goto REPLOOP


!=================================
!First round of math to check if in TSE
!=================================

set SUMHBI 0
set I 0
LABEL LOOP

open read unit 30 card name "final_@I.crd"
read coor unit 30 card
close unit 30

! Modify to fit either one

QUICK %Selection_param1 !sele atom A 81 C7 end sele atom W 277 OH2 end
calc CARWATDIST = ?DIST

QUICK %Selection_param2 !sele atom A 81 OG end sele atom A 81 C7 end
calc CAROGDIST = ?DIST

calc COOR = @CAROGDIST - @CARWATDIST

set HBI 0

IF COOR .GE. @LOWERBBASIN THEN set HBI 1	
IF COOR .GE. @HIGHERBBASIN THEN set HBI 0

INCREMENT SUMHBI by @HBI

INCREMENT I by 1
if I .LT. @NMIN THEN GOTO LOOP
	

calc PROBB = @SUMHBI/@NMIN
calc SIGMA = SQRT(@PROBB*(1-@PROBB)/@NMIN)

calc LOWER = @PROBB - @ALPHA*@SIGMA
calc HIGHER = @PROBB + @ALPHA*@SIGMA

if LOWER .GT. 0.5 THEN STOP
if HIGHER .LT. 0.5 THEN STOP

!=================================
!NMAX trajectories dynamics
!=================================

label RLOOP

open read unit 30 card name "trajreadin.crd"
read coor unit 30 card
close unit 30

RAND OLDR
calc theseed = ?rand * %Random_inp_num

open write unit 341 card name rsum1_@COUNTER.txt
open write unit 342 card name rsum2_@COUNTER.txt

Dynamics Leap -
       finalt 300 firstt 300 ichecw 0 ihbfrq 0 ilbfrq 0 -
       inbfrq -1 iprfrq 10 iscale 0 iseed @theseed iuncrd -1 -
       iunread -1 iunvelo -1 iunwrite -1 nprint 50 nsavcrd 1 -
       nsavvelo 0 nstep @STEPNUM scale 1 tbath 300 teminc 0 -
       timestp 0.001 twindh 0 twindl 0 

open write unit 30 card name "final_@COUNTER.crd"
write coor unit 30 card
close unit 30

close unit 341
close unit 342

	!=================================
	!Check trajectory completion and PROBB
	!=================================

QUICK %Selection_param1 !sele atom A 81 C7 end sele atom W 277 OH2 end
calc CARWATDIST = ?DIST

QUICK %Selection_param2 !sele atom A 81 OG end sele atom A 81 C7 end
calc CAROGDIST = ?DIST

calc COOR = @CAROGDIST - @CARWATDIST

set HBI 0

IF COOR .GE. @LOWERBBASIN THEN set HBI 1	
IF COOR .GE. @HIGHERBBASIN THEN set HBI 0

INCREMENT SUMHBI by @HBI

calc CURRN = @COUNTER + 1

calc MIDPROBB = @SUMHBI/@CURRN
calc MIDSIGMA = SQRT(@MIDPROBB*(1-@MIDPROBB)/@CURRN)

calc MIDLOWER = @MIDPROBB - @ALPHA*@MIDSIGMA
calc MIDHIGHER = @MIDPROBB + @ALPHA*@MIDSIGMA

if MIDLOWER .GT. 0.5 THEN STOP
if MIDHIGHER .LT. 0.5 THEN STOP

increment COUNTER by 1
if COUNTER .LT. @NMAX goto RLOOP

!=================================
!Final Calculation of PROBB, should be the same as last MIDPROBB
!=================================

calc BIGPROBB = @SUMHBI/@NMAX
calc BIGSIGMA = SQRT(@BIGPROBB*(1-@BIGPROBB)/@NMAX)

calc BIGLOWER = @BIGPROBB - @ALPHA*@BIGSIGMA
calc BIGHIGHER = @BIGPROBB + @ALPHA*@BIGSIGMA

if BIGLOWER .GT. 0.5 THEN STOP
if BIGHIGHER .LT. 0.5 THEN STOP

ECHO @BIGPROBB
ECHO @BIGSIGMA
ECHO @BIGLOWER
ECHO @BIGHIGHER

stop
