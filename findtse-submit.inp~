

bomlev -2

!=================================
!Read topology and parameter files
!=================================

open unit 1 form read name "%topology_file"
read rtf card unit 1
close unit 1

open unit 1 form read name "%parameter_file"
read param card unit 1
close unit 1


set temp = 300


!=================================
!Read current psf and coordinates
!=================================

open read unit 30 card name "%psf_file"
read psf unit 30 card
close unit 30

open read unit 30 card name "%crd_file"
read coor unit 30 card
close unit 30

open read unit 47 file name "%dcd_read_in_file"
trajectory query unit 47
calc ntst = ?skip
calc begst = @ntst*%frame_to_read
calc stopst = @ntst*%frame_to_read
trajectory firstu 47 nunit 1 skip @ntst begin @begst stop @stopst
traj read
close unit 47

open write unit 30 card name "trajreadin.crd"
write coor unit 30 card
close unit 30

!=================================
!Set quantum regions
!=================================

QUANTUM SELECT ( -
  ((resi 80) .and. (type C .or. type O)) .or. -
  ((resi 81)) .or. -
  ((resi 82) .and. (type N .or. type HN)) .or. -
  ((resi 84) .and. (type CH .or. type OH1 .or. type OH2 .or. type NZ .or. type HZ1  -
	.or. type CE .or. type HE1 .or. type HE2 .or. type CD .or. type HD1 -
	.or. type HD2 .or. type CG .or. type HG1 .or. type HG2)) .or. -
  ((resi 128) .and. (type HG1 .or. type OG .or. type CB .or. type HB1 -
	.or. type HB2)) .or. -
  ((resi 167) .and. (type CG .or. type CD1 .or. type HD1 .or. type NE1 .or. type HE1 -
	.or. type CE2 .or. type CZ2 .or. type HZ2 .or. type CH2 .or. type HH2 -
	.or. type CZ3 .or. type HZ3 .or. type CE3 .or. type HE3 .or. type CD2)) .or. -
  ((resi 218) .and. (type NZ .or. type HZ1 .or. type HZ2 .or. type HZ3 .or. type CE -
	.or. type HE1 .or. type HE2 .or. type CD .or. type HD1 .or. type HD2 .or. -
	type CG .or. type HG1 .or. type HG2)) .or. -
  ((resi 220) .and. (type C .or. type O)) .or. -
  ((resi 221) .and. (type C .or. type O .or. type CA .or. type HA .or. type N .or. type HN)) .or. -
  ((resi 222) .and. (type N .or. type HN)) .or. -
  ((resi 276)) .or. -
  ((resi 277)) .or. -
  ((resi 80) .and. (type CA)) .or. -
  ((resi 82) .and. (type CA)) .or. -
  ((resi 84) .and. (type CB)) .or. -
  ((resi 128) .and. (type CA)) .or. -
  ((resi 167) .and. (type CB)) .or. -
  ((resi 218) .and. (type CB)) .or. -
  ((resi 220) .and. (type CA)) .or. -
  ((resi 221) .and. (type CB)) .or. -
  ((resi 222) .and. (type CA))) end -
glnk select ( -
  ((resi 80) .and. (type CA)) .or. -
  ((resi 82) .and. (type CA)) .or. -
  ((resi 84) .and. (type CB)) .or. -
  ((resi 128) .and. (type CA)) .or. -
  ((resi 167) .and. (type CB)) .or. -
  ((resi 218) .and. (type CB)) .or. -
  ((resi 220) .and. (type CA)) .or. -
  ((resi 221) .and. (type CB)) .or. -
  ((resi 222) .and. (type CA))) end -
charge 0 -
remove am1


!=================================
!Set quantum regions
!=================================

rxncor: define car point select atom A 81 C7 end
rxncor: define wat point select atom W 277 OH2 end
rxncor: define og point select atom A 81 OG end
rxncor: define prot point select atom W 277 H1 end
rxncor: define kcx point select atom A 84 OH1 end

rxncor: define otemp distance car wat
rxncor: define twtmp distance car og
rxncor: define thtmp distance prot kcx

rxncor: define rone scomb twtmp 1 otemp -1
rxncor: define rtwo scomb thtmp -1 thtmp 0

rxncor: set nrxn 2 rone rtwo


RAND OLDR
calc theseed = ?rand * %number_to_calc_the_seed


!=================================
!Set up umbrella center with a zero force constant so we can trace it
!=================================

rxncor: umbr name rone kumb 0 del0 0 form 1
rxncor: umbr name rtwo kumb 0 del0 0 form 1



!=================================
!Forward propogation of dynamics
!=================================

open write unit 341 card name rsum1_for.txt
open write unit 342 card name rsum2_for.txt

rxncor: trace rone unit 341 freq 1
rxncor: trace rtwo unit 342 freq 1

open write formatted unit 19 name "trajprop_for.rst"
Dynamics Leap -
       finalt 300 firstt 300 ichecw 0 ihbfrq 0 ilbfrq 0 -
       inbfrq -1 iprfrq 10 iscale 0 iseed @theseed iuncrd -1 -
       iunread -1 iunvelo -1 iunwrite 19 nprint 50 nsavcrd 1 -
       nsavvelo 0 nstep 1 scale 1 tbath 300 teminc 0 -
       timestp 0.001 twindh 0 twindl 0 
close unit 19

open write unformatted unit 46 name "trajprop_for.vel"
open read formatted unit 22 name "trajprop_for.rst"
open write unformatted unit 45 name "trajprop_for.dcd"
Dynamics Leap Restart-
       finalt 300 firstt 300 ichecw 0 ihbfrq 0 ilbfrq 0 -
       inbfrq -1 iprfrq 10 iscale 0 iseed @theseed iuncrd 45 -
       iunread 22 iunvelo 46 iunwrite -1 nprint 50 nsavcrd 1 -
       nsavvelo 1 nstep 1000 scale 1 tbath 300 teminc 0 -
       timestp 0.001 twindh 0 twindl 0 
close unit 46
close unit 22
close unit 45

open write unit 30 card name "tpstraj_for_final.crd"
write coor unit 30 card
close unit 30

close unit 341
close unit 342


!=================================
!Reverse propogation of dynamics
!=================================

open read unit 30 card name "trajreadin.crd"
read coor unit 30 card
close unit 30

open write unit 341 card name rsum1_rev.txt
open write unit 342 card name rsum2_rev.txt

open write unformatted unit 46 name "trajprop_rev.vel"
open read formatted unit 22 name "trajprop_for.rst"
open write unformatted unit 45 name "trajprop_rev.dcd"
Dynamics Leap Restart-
       finalt 300 firstt 300 ichecw 0 ihbfrq 0 ilbfrq 0 -
       inbfrq -1 iprfrq 10 iscale 1 iseed @theseed iuncrd 45 -
       iunread 22 iunvelo 46 iunwrite -1 nprint 50 nsavcrd 1 -
       nsavvelo 1 nstep 1000 scale -1 tbath 300 teminc 0 -
       timestp 0.001 twindh 0 twindl 0 
close unit 46
close unit 22
close unit 45

open write unit 30 card name "tpstraj_rev_final.crd"
write coor unit 30 card
close unit 30

close unit 341
close unit 342

stop
